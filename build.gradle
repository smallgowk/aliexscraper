plugins {
  id 'java'
  id 'application'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
}

group 'com.phanduy.aliexscrap'
version '1.0'

task createProperties(dependsOn: processResources) {
  doLast {
    new File("$buildDir/resources/main/app.properties").withWriter { w ->
      Properties p = new Properties()
      p['version'] = project.version.toString()
      p.store w, null
    }
  }
}


classes {
  dependsOn createProperties
}

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.10.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}


application {
  mainModule = 'com.phanduy.aliexscrap.aliexscrapper'
  mainClass = 'com.phanduy.aliexscrap.AliexScrapperClientApp'
  applicationDefaultJvmArgs = [
          '--add-modules', 'javafx.controls,javafx.fxml',
          '--add-opens', 'javafx.controls/javafx.scene.control=ALL-UNNAMED'
  ]
}

javafx {
  version = '17.0.10'
  modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing' ]
}

dependencies {
  implementation("org.openjfx:javafx-controls:17.0.10")
  implementation("org.openjfx:javafx-fxml:17.0.10")
  implementation("org.openjfx:javafx-web:17.0.10")
  implementation("org.openjfx:javafx-swing:17.0.10")

//   UI Libraries
  implementation("org.controlsfx:controlsfx:11.1.2")
  implementation("com.dlsc.formsfx:formsfx-core:11.3.2")
  implementation("net.synedra:validatorfx:0.4.0")
  implementation("org.kordamp.ikonli:ikonli-javafx:12.3.1")
  implementation("org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0")
  implementation("eu.hansolo:tilesfx:11.48")

  // REST / JSON / Retrofit
  implementation("com.squareup.okhttp3:okhttp:4.12.0")
  implementation("com.squareup.okhttp3:logging-interceptor:4.12.0")
  implementation("org.json:json:20230618")
  implementation("com.fasterxml.jackson.core:jackson-databind:2.17.0")
  implementation("com.squareup.retrofit2:retrofit:2.9.0")
  implementation("com.squareup.retrofit2:converter-gson:2.9.0")

  // Excel
  implementation("org.apache.poi:poi-ooxml:5.2.3")
  implementation("org.apache.commons:commons-collections4:4.4")
  implementation("org.apache.commons:commons-compress:1.23.0")

  // JAXB
  implementation("javax.xml.bind:jaxb-api:2.3.1")
}

test {
useJUnitPlatform()}

jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'app'
  }
  jpackage {
    imageName = 'AliexScrap'
    installerName = 'AliexScrapInstaller'
    installerType = 'exe' // hoặc dmg, pkg, deb, rpm, app-image...

    icon = file('src/main/resources/image/aliexscrap.ico').absolutePath

    // Tuỳ chọn bổ sung:
    appVersion = '1.0.0'
    installerOptions = [
            '--win-menu',
            '--win-shortcut',
            '--win-dir-chooser'
    ]
  }

}

jlinkZip {
  group = 'distribution'
}